apply plugin: 'java-library'

group 'org.comroid'
version '0.3.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.findByName('wrapper').configure {
    gradleVersion = '6.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply from: "gradle/vars.gradle"

tasks.withType(Javadoc) {
    source = sourceSets.main.java

    options {
        encoding = 'UTF-8'
        /*
        links = [
                "https://docs.oracle.com/javase/8/docs/api/",
                "https://docs.oracle.com/javaee/7/api/"
        ]
        */
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'

    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    maven {
        url "https://maven.jetbrains.space/comroid/releases"
        credentials.username "Anonymous.User"
        credentials.password "anonymous"
    }
    jcenter()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:20.1.0'

    implementation 'org.comroid:common-util:1.5.4'
    implementation 'org.comroid:mutatio:0.10.1'
    implementation 'org.comroid:uniform:0.16.0'
    implementation 'org.comroid:varbind:0.20.0'
    implementation 'org.comroid:restless:0.15.2'
    implementation 'org.comroid:dreadpool:0.10.0'

    implementation 'com.google.flogger:flogger:0.5.1'
}

apply from: "gradle/publishing.gradle"
