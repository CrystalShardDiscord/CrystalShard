buildscript {
    repositories.gradlePluginPortal()
    dependencies.classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
}

apply plugin: 'java-library'

group 'de.comroid'
version '1.0.0-SNAPSHOT'

apply from: 'gradle/vars.gradle'

sourceCompatibility = 13
targetCompatibility = 13

tasks.findByName('wrapper').configure {
    gradleVersion = '5.6'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'

    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

allprojects {
    tasks.withType(Javadoc) {
        source = sourceSets.main.java
        
        options {
            encoding = 'UTF-8'
            links = [
                    "https://docs.oracle.com/javase/8/docs/api/",
                    "https://docs.oracle.com/javaee/7/api/"
            ]
        }
    }
}

compileJava.options.encoding = 'UTF-8'
repositories.jcenter()

dependencies {
    api project(':api')
    api project(':util')
    
    runtimeOnly project(':impl')
    runtimeOnly project(':core')

    // test configuration
    testImplementation project(':api')
    testImplementation project(':util')
    testImplementation project(':core')
    testImplementation project(':impl')
}

sourceSets {
    main.java.srcDirs = []
    main.resources.srcDirs = []
    test.java.srcDirs = ["src/test/java"]
    test.resources.srcDirs = ["src/test/resources"]
}

subprojects {
    apply plugin: 'java-library'

    group 'de.kaleidox'

    sourceCompatibility = 11
    targetCompatibility = 11

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava

        try {
            archiveClassifier.set 'sources'
        } catch (MissingPropertyException ignored) {
            classifier = "sources"
        }
    }

    task javadocJar(type: Jar) {
        dependsOn 'javadoc'

        from javadoc.destinationDir

        try {
            archiveClassifier.set 'javadoc'
        } catch (MissingPropertyException ignored) {
            classifier = "javadoc"
        }
    }

    compileJava.options.encoding = 'UTF-8'
    repositories.jcenter()

    dependencies {
        implementation 'com.google.flogger:flogger:0.4'
        compileOnly 'org.jetbrains:annotations:18.0.0'

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.easymock:easymock:4.1'
        testImplementation 'com.google.flogger:flogger-system-backend:0.4'
        testCompileOnly 'org.codehaus.groovy:groovy:3.0.0-rc-2'
    }
}

project(":api") {
    version = '1.0.0-SNAPSHOT'

    dependencies {
        compileOnly 'com.alibaba:fastjson:1.2.62'

        testImplementation 'org.reflections:reflections:0.9.11'
    }
}

project(":core") {
    version = '1.0.0-SNAPSHOT'

    dependencies {
        api project(':api')

        implementation 'com.alibaba:fastjson:1.2.62'

        runtimeOnly 'com.google.flogger:flogger-system-backend:0.4'
    }
}

project(":impl") {
    version = '1.0.0-SNAPSHOT'

    dependencies {
        api project(':api')
        compileOnly project(':core')

        implementation 'com.alibaba:fastjson:1.2.62'

        runtimeOnly 'com.google.flogger:flogger-system-backend:0.4'
    }
}

project(":util") {
    version = '1.0.0-SNAPSHOT'
    
    dependencies {
        api project(':api')
        compileOnly project(':core')

        implementation 'com.alibaba:fastjson:1.2.62'

        runtimeOnly 'com.google.flogger:flogger-system-backend:0.4'
    }
}

task allJavadoc(type: Javadoc) {
    source exportedProjects.collect { //noinspection GroovyAssignabilityCheck
        project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { //noinspection GroovyAssignabilityCheck
        project(it).sourceSets.main.compileClasspath })
    options.encoding = 'UTF-8'
    options.destinationDirectory(file("./docs/"))
}

apply from: 'gradle/publishing.gradle'
