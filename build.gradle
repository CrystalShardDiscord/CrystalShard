buildscript {
    repositories.gradlePluginPortal()
    dependencies.classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.0"
}

apply plugin: 'java-library'

group 'de.comroid'
version '1.0.0-SNAPSHOT'

apply from: 'gradle/vars.gradle'

sourceCompatibility = targetCompatibility = 1.8

tasks.findByName('wrapper').configure {
    gradleVersion = '5.6'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'

    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

allprojects {
    tasks.withType(Javadoc) {
        source = sourceSets.main.java
        
        options {
            encoding = 'UTF-8'
            links = [
                    "https://docs.oracle.com/javase/8/docs/api/",
                    "https://docs.oracle.com/javaee/7/api/"
            ]
        }
    }
}

compileJava.options.encoding = 'UTF-8'
repositories.jcenter()

dependencies {
    api project(':api')
    runtimeOnly project(':impl')
    runtimeOnly project(':core')
}

sourceSets {
    main.java.srcDirs = []
    main.resources.srcDirs = []
    test.java.srcDirs = []
    test.resources.srcDirs = []
}

subprojects {
    apply plugin: 'java-library'

    group 'de.kaleidox'

    sourceCompatibility = 13
    targetCompatibility = 13

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava

        try {
            archiveClassifier.set 'sources'
        } catch (MissingPropertyException ignored) {
            classifier = "sources"
        }
    }

    task javadocJar(type: Jar) {
        dependsOn 'javadoc'

        from javadoc.destinationDir

        try {
            archiveClassifier.set 'javadoc'
        } catch (MissingPropertyException ignored) {
            classifier = "javadoc"
        }
    }

    compileJava.options.encoding = 'UTF-8'
    repositories.jcenter()

    dependencies {
        implementation 'com.google.flogger:flogger:0.4'
        compileOnly 'org.jetbrains:annotations:18.0.0'

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.easymock:easymock:4.1'
        testImplementation 'com.google.flogger:flogger-system-backend:0.4'
    }
}

project(":api") {
    version = '2.0.0'

    dependencies {
        compileOnly 'com.alibaba:fastjson:1.2.62'

        testImplementation 'org.reflections:reflections:0.9.11'
    }
}

project(":core") {
    version = '2.0.0'

    dependencies {
        api project(':api')

        implementation 'com.alibaba:fastjson:1.2.62'

        runtimeOnly 'com.google.flogger:flogger-system-backend:0.4'
    }
}

project(":impl") {
    version = '2.0.0'

    dependencies {
        api project(':api')
        api project(':core')

        implementation 'com.alibaba:fastjson:1.2.62'

        runtimeOnly 'com.google.flogger:flogger-system-backend:0.4'
    }
}

project(":util") {
    version = '2.0.0'
}

task allJavadoc(type: Javadoc) {
    source exportedProjects.collect { //noinspection GroovyAssignabilityCheck
        project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { //noinspection GroovyAssignabilityCheck
        project(it).sourceSets.main.compileClasspath })
    options.encoding = 'UTF-8'
    options.destinationDirectory(file("./docs/"))
}

apply from: 'gradle/publishing.gradle'
